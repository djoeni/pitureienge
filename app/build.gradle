plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}


def keystoreFile = rootProject.file("keystore.properties")
def keystore = new Properties()
keystore.load(new FileInputStream(keystoreFile))

android {
    compileSdkVersion Integer.parseInt("$compileSdkVer")
    buildToolsVersion "$buildToolsVer"

    compileOptions {
        targetCompatibility = "8"
        sourceCompatibility = "8"
    }

    defaultConfig {
        applicationId "com.gatcha.ang"
        minSdkVersion 21
        targetSdkVersion Integer.parseInt("$targetSdkVer")
        multiDexEnabled true
        versionCode 510
        versionName "1.8.2-18"

        setProperty("archivesBaseName", "GatchaNG-$versionName")
    }

    signingConfigs {
        release {
            storeFile rootProject.file("release.keystore")
            keyAlias keystore['keyAlias']
            keyPassword keystore['keyPassword']
            storePassword keystore['storePassword']
        }
    }

    buildTypes {
        named("release") {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.findByName("release")
        }
        named("debug") {
            versionNameSuffix = ".debug"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            java.srcDirs += 'src/main/kotlin'
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    splits {
        abi {
            enable true
            reset()
            include("armeabi-v7a", "arm64-v8a", "x86", "x86_64")
            universalApk true
        }
    }

    buildFeatures {
        viewBinding true
    }
    namespace 'com.gatcha.ang'
    testNamespace 'com.gatcha.angTest'

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'], exclude: [])
    testImplementation 'junit:junit:4.13.2'

    // Androidx
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.fragment:fragment-ktx:1.5.6'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.viewpager2:viewpager2:1.1.0-beta01'

    // Androidx ktx
    implementation 'androidx.activity:activity-ktx:1.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'

    //kotlin
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.8.0"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    implementation 'com.tencent:mmkv-static:1.2.15'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'io.reactivex:rxjava:1.3.8'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.tbruyelle.rxpermissions:rxpermissions:0.9.4@aar'
    implementation 'com.github.jorgecastilloprz:fabprogresscircle:1.01@aar'
    implementation 'me.drakeet.support:toastcompat:1.1.0'
    implementation 'com.blacksquircle.ui:editorkit:2.2.0'
    implementation 'com.blacksquircle.ui:language-base:2.2.0'
    implementation 'com.blacksquircle.ui:language-json:2.2.0'
    implementation 'io.github.g00fy2.quickie:quickie-bundled:1.6.0'
    implementation 'com.google.zxing:core:3.5.1'
}
